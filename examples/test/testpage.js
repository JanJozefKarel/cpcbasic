/* globals cpcBasic */

"use strict";

cpcBasic.addItem("", function () { /*
10 REM Test Page
20 REM Marco Vieth, 2019-2020
30 '
40 '
50 '|renum,100,100,10,9000:stop
60 '
100 '
110 MODE 2:'comment
120 '
130 PRINT"Numbers"
140 a=1:IF a<>1 THEN ERROR 33
150 a%=1:IF a%<>1 THEN ERROR 33
160 a=1.2:IF a<>1.2 THEN ERROR 33
170 a!=1.2:IF a!<>1.2 THEN ERROR 33
180 a!=-1.2:IF a!<>-1.2 THEN ERROR 33
190 a=1200:IF a<>1200 THEN ERROR 33
200 a%=1200:IF a%<>1200 THEN ERROR 33
210 a=-7.2:IF a<>-7.2 THEN ERROR 33
220 a%=-7.2:IF a%<>-7 THEN ERROR 33
230 a=+7.2:IF a<>7.2 THEN ERROR 33
240 a=0.2:IF a<>0.2 THEN ERROR 33
250 a=2:IF a<>2 THEN ERROR 33
260 a=10000:IF a<>10000 THEN ERROR 33
270 a=0.0001:IF a<>0.0001 THEN ERROR 33
280 PRINT"hex number: &, &h"
290 a=&A7:IF a<>167 THEN ERROR 33
300 a%=&A7:IF a%<>167 THEN ERROR 33
310 a%=-&A7:IF a%<>-167 THEN ERROR 33
320 a%=&7FFF:IF a%<>32767 THEN ERROR 33
330 PRINT"bin number: &x"
340 a=&X10100111:IF a<>167 THEN ERROR 33
350 a%=&X10100111:IF a%<>167 THEN ERROR 33
360 a%=&X111111111111111:IF a%<>32767 THEN ERROR 33
370 a%=-&X111111111111111:IF a%<>-32767 THEN ERROR 33
380 PRINT"Strings"
390 a$="a12":IF a$<>"a12" THEN ERROR 33
400 a$=+"7.1":IF a$<>"7.1" THEN ERROR 33
410 '
420 PRINT"Variable types"
430 a!=1.4:IF a!<>1.4 THEN ERROR 33
440 a!=1.5:IF a!<>1.5 THEN ERROR 33
450 a%=1.4:IF a%<>1 THEN ERROR 33
460 a%=1.5:IF a%<>2 THEN ERROR 33
470 a$="1.4":IF a$<>"1.4" THEN ERROR 33
480 insert.line=2:IF insert.line<>2 THEN ERROR 33
490 '
500 PRINT"Array Variables"
510 a!(2)=1.4:IF a!(2)<>1.4 THEN ERROR 33
520 a!(2)=1.5:IF a!(2)<>1.5 THEN ERROR 33
530 a%(2)=1.4:IF a%(2)<>1 THEN ERROR 33
540 a%(2)=1.5:IF a%(2)<>2 THEN ERROR 33
550 a$(2)="1.4":IF a$(2)<>"1.4" THEN ERROR 33
560 '
570 PRINT"expressions, operators +-*..."
580 a%=1+2+3:IF a%<>6 THEN ERROR 33
590 a%=3-2-1:IF a%<>0 THEN ERROR 33
600 a%=&A7+&X10100111-(123-27):IF a%<>238 THEN ERROR 33
610 a%=3+2*3-7:IF a%<>2 THEN ERROR 33
620 a%=(3+2)*(3-7):IF a%<>-20 THEN ERROR 33
630 a=20/2.5:IF a<>8 THEN ERROR 33
640 a=20\3:IF a<>6 THEN ERROR 33
650 a=3^2:IF a<>9 THEN ERROR 33
660 a=&X1001 AND &X1110:IF a<>&X1000 THEN ERROR 33
670 a=&X1001 OR &X110:IF a<>&X1111 THEN ERROR 33
680 a=&X1001 XOR &X1010:IF a<>&X11 THEN ERROR 33
690 a=+++++++++---9:IF a<>-9 THEN ERROR 33
700 a=(1=0):IF a<>0 THEN ERROR 33
710 a=(1>0)*(0<1):IF a<>1 THEN ERROR 33
720 GOSUB 9040
730 '
740 PRINT"ABS(positive number)"
750 a=ABS(+67.98):IF a<>67.98 THEN ERROR 33
760 a!=ABS(+67.98):IF a!<>67.98 THEN ERROR 33
770 PRINT"ABS(negative number)"
780 a=ABS(-67.98):IF a<>67.98 THEN ERROR 33
790 PRINT"ABS(0)"
800 a=ABS(0):IF a<>0 THEN ERROR 33
810 '
820 PRINT"@ (address of)": 'CPCBasic: just return internal variable index
830 CLEAR
840 a=7:PRINT@a;@a^2;@a+1*2
850 b=8:PRINT@b;@a(0)
860 '
870 PRINT"AND (and OR)"
880 a=4 OR 7 AND 2:IF a<>6 THEN ERROR 33
890 a%=4 OR 7 AND 2:IF a%<>6 THEN ERROR 33
900 '
910 PRINT"ASC"
920 a=ASC("a"):IF a<>97 THEN ERROR 33
930 a=ASC("ab"):IF a<>97 THEN ERROR 33
940 ON ERROR GOTO 950:a=ASC(""):PRINT"Error expected!":ERROR 33: 'expect error 5
950 IF ERR<>5 THEN PRINT"err=";ERR;"erl=";ERL:ERROR 33 ELSE RESUME 960
960 ON ERROR GOTO 0
970 ON ERROR GOTO 980:a=ASC(0):PRINT"Error expected!":ERROR 33: 'expect error 13
980 IF ERR<>13 THEN PRINT"err=";ERR;"erl=";ERL:ERROR 33 ELSE RESUME 990
990 ON ERROR GOTO 0
1000 '
1010 'cls#0: a=2: cls #(a*3)
1020 PRINT"COPYCHR$"
1030 PRINT"Detect char 143 with matching pen"
1040 PRINT CHR$(143);"#";:PAPER 1
1050 GOSUB 9010:PAPER 0:IF a$<>CHR$(143)+"#" THEN PRINT"Check: Different on real CPC!":IF a$<>" #" THEN ERROR 33
1060 PRINT"Detect char 130 with matching paper as char 141"
1070 PEN 0:PAPER 1:PRINT CHR$(130);"#";:PEN 1
1080 GOSUB 9010:PAPER 0:IF a$<>CHR$(141)+"#" THEN PRINT"Check: Different on real CPC!":IF a$<>CHR$(130)+"#" THEN ERROR 33
1090 PRINT"Detect char 143 with matching paper as char 32"
1100 PEN 0:PAPER 1:PRINT CHR$(143);"#";:PEN 1
1110 GOSUB 9010:PAPER 0:IF a$<>" #" THEN ERROR 33
1120 '
1130 PRINT"DATA with spaces between arguments"
1140 b$="":RESTORE 1160
1150 READ a$:b$=b$+a$:IF a$<>"-1" THEN GOTO 1150
1160 DATA ",", "abc"  , xy, -1
1170 IF b$<>",abcxy-1" THEN ERROR 33
1180 PRINT"DATA with special characters"
1190 b$="":RESTORE 1200
1200 DATA " ",!"#$%&'()*+,","
1210 FOR i=1 TO 3:READ a$:b$=b$+a$:NEXT
1220 IF b$<>" !"+CHR$(34)+"#$%&'()*+," THEN ERROR 33
1230 PRINT"DATA is interpeted depending on variable type"
1240 DATA 001.6,001.6, 001.6
1250 READ a%,a!,a$
1260 IF a%<>2 THEN ERROR 33
1270 IF a!<>1.6 THEN ERROR 33
1280 IF a$<>"001.6" THEN ERROR 33
1290 DATA &a7, &ha7, &x10100111
1300 READ a%:IF a%<>&A7 THEN ERROR 33
1310 READ a%:IF a%<>&A7 THEN ERROR 33
1320 READ a%:IF a%<>&A7 THEN ERROR 33
1330 PRINT"DATA with empty parameters"
1340 b$="":RESTORE 1440
1350 FOR i=1 TO 16:READ a$:b$=b$+a$:NEXT
1360 IF b$<>"1a11b1#" THEN ERROR 33
1370 PRINT"DATA reading empty as numbers"
1380 b$="":RESTORE 1440
1390 FOR i=1 TO 16
1400 IF i=8 OR i=13 OR i=16 THEN READ a$ ELSE READ a:a$=STR$(a)
1410 b$=b$+a$
1420 NEXT
1430 IF b$<>" 0 0 0 0 0 0 1a 0 1 1 0b 1 0#" THEN ERROR 33
1440 DATA
1450 DATA ,
1460 DATA ,,
1470 DATA 1
1480 DATA a
1490 DATA ,1
1500 DATA 1,
1510 DATA   b   ,   1   ,
1520 DATA #
1530 '
1540 PRINT"DEC$(number,format)"
1550 a$=DEC$(8.575,"##.##"):IF a$<>" 8.58" THEN ERROR 33
1560 a$=DEC$(15.35,"#.##"):IF a$<>"%15.35" THEN ERROR 33
1570 '
1580 PRINT"DEF FN"
1590 DEF FNf1(x)=x*x
1600 a=FNf1(2.5):IF a<>6.25 THEN ERROR 33
1610 a=FN f1(2.5):IF a<>6.25 THEN ERROR 33
1620 DEF FN f1%(x)=x*x
1630 a%=FNf1%(2.5):IF a%<>6 THEN ERROR 33
1640 a%=FN f1%(2.5):IF a%<>6 THEN ERROR 33
1650 a=FNf1%(2.5):IF a<>6 THEN ERROR 33
1660 DEF FN f1!(x)=x*x
1670 a!=FNf1!(2.5):IF a!<>6.25 THEN ERROR 33
1680 a!=FN f1!(2.5):IF a!<>6.25 THEN ERROR 33
1690 a=FNf1!(2.5):IF a<>6.25 THEN ERROR 33
1700 DEF FN f1$(x$)=x$+x$
1710 a$=FNf1$("a"):IF a$<>"aa" THEN ERROR 33
1720 a$=FN f1$("a"):IF a$<>"aa" THEN ERROR 33
1730 DEF FNf2=2.5*2.5
1740 a=FNf2:IF a<>6.25 THEN ERROR 33
1750 DEF FN f2%=2.5*2.5
1760 a%=FN f2%:IF a%<>6 THEN ERROR 33
1770 '
1780 PRINT"ELSE"
1790 a=1 ELSE a=2
1800 ELSE a=3
1810 IF a<>1 THEN ERROR 33
1820 '
1830 PRINT"ERASE"
1840 ERASE a: 'a was used in previous tests
1850 DIM a(4):FOR i=0 TO 4:a(i)=i:NEXT
1860 a=0:FOR i=0 TO 4:a=a+a(i):NEXT:IF a<>10 THEN ERROR 33
1870 ERASE a
1880 IF a<>10 THEN ERROR 33
1890 a=0:FOR i=0 TO 4:a=a+a(i):NEXT:IF a<>0 THEN ERROR 33
1900 GOSUB 9040
1910 '
1920 PRINT"FIX"
1930 a=FIX(1.5)
1940 IF a<>1 THEN ERROR 33
1950 a=FIX(-1.5)
1960 IF a<>-1 THEN ERROR 33
1970 '
1980 PRINT"FOR with integer constants"
1990 a$="":FOR i=+4 TO 0 STEP -2:a$=a$+STR$(i):NEXT:IF a$<>" 4 2 0" THEN ERROR 33
2000 a=0:FOR i=++4 TO 1 STEP ---2:a=a+i:NEXT:IF a<>6 THEN ERROR 33: 'avoid ++ and -- in js!
2010 PRINT"FOR with integer variable and floating point ranges"
2020 a=0:FOR i%=1.2 TO 9.7 STEP 3.2:a=a+i%:NEXT:IF a<>22 THEN ERROR 33: '1+4+7+10
2030 PRINT"FOR with condition expressions"
2040 a=3:FOR i=a<>3 TO a>=3 STEP a=3:PRINT i;:NEXT:PRINT "#";
2050 GOSUB 9010:IF a$<>" 0 -1 #" THEN ERROR 33
2060 PRINT"FOR up to 2*PI"
2070 a=13/8*PI:FOR i=1 TO 3:a=a+1/8*PI:NEXT:IF a>2*PI THEN PRINT"limit exceeded by";a-2*PI;"(TODO)" ELSE PRINT"ok"
2080 'gosub 9040
2090 '
2100 PRINT"GOTO with leading zeros"
2110 GOTO 2120
2120 PRINT"ok"
2130 PRINT"INSTR"
2140 a=INSTR("Amstrad", "m"):IF a<>2 THEN ERROR 33
2150 a=INSTR("Amstrad", "sr"):IF a<>0 THEN ERROR 33
2160 '
2170 PRINT"ON n GOSUB"
2180 a=0:ON 1 GOSUB 2250,2260:IF a<>1 THEN ERROR 33
2190 a=0:ON 2 GOSUB 2250,2260:IF a<>2 THEN ERROR 33
2200 a=0:ON 1.5 GOSUB 2250,2260:IF a<>2 THEN ERROR 33
2210 a=1.5:ON a GOSUB 2250,2260:IF a<>2 THEN ERROR 33
2220 a=0:ON 3 GOSUB 2250,2260:IF a<>0 THEN ERROR 33
2230 a=0:ON 0 GOSUB 2250,2260:IF a<>0 THEN ERROR 33
2240 GOTO 2270
2250 a=1:RETURN
2260 a=2:RETURN
2270 PRINT"ON n GOTO"
2280 a=1.7:ON a-0.2 GOTO 2300,2310
2290 GOTO 2320
2300 a=1:GOTO 2320
2310 a=2:GOTO 2320
2320 IF a<>2 THEN ERROR 33
2330 GOSUB 9040
2340 '
2350 PRINT"PRINT in FOR loop"
2360 FOR i=1 TO 5:PRINT i;:NEXT i:PRINT"#";
2370 GOSUB 9010:IF a$<>" 1  2  3  4  5 #" THEN ERROR 33
2380 PRINT"PRINT in GOTO loop"
2390 a=1
2400 PRINT a;: a=a+1: IF a <= 5 THEN GOTO 2400 ELSE PRINT "#";
2410 GOSUB 9010:IF a$<>" 1  2  3  4  5 #" THEN ERROR 33
2420 PRINT"PRINT in WHILE loop"
2430 a=1: WHILE a<=5: PRINT a;: a=a+1: WEND: PRINT "#";
2440 GOSUB 9010:IF a$<>" 1  2  3  4  5 #" THEN ERROR 33
2450 PRINT"PRINT concatenated string"
2460 a=1: s$="": WHILE a<=5: s$=s$+STR$(a)+":": a=a+1: b=0: WHILE b<3: b=b+1: s$=s$+STR$(b): WEND: s$=s$+" ": WEND: s$=s$+"#":PRINT s$;
2470 GOSUB 9010:IF a$<>" 1: 1 2 3  2: 1 2 3  3: 1 2 3  4: 1 2 3  5: 1 2 3 #" THEN ERROR 33
2480 '
2490 PRINT"IF THEN ELSE: WEND in ELSE"
2500 a=0: WHILE a<5: a=a+1: IF a=3 OR 3=a THEN PRINT "a=";a;"(three) "; ELSE PRINT "a<>3:";a;"(not three) ";: WEND : PRINT"after WEND": 'WEND in ELSE
2510 PRINT"#";
2520 GOSUB 9010:IF a$<>"a<>3: 1 (not three) a<>3: 2 (not three) a= 3 (three) #" THEN ERROR 33
2530 '
2540 GOSUB 9040
2550 PRINT"PRINT numbers separated by space"
2560 PRINT 123;:PRINT"#";
2570 GOSUB 9010: 'if a$<>" 1  2  3 #" then error 33: 'not ok! On real CPC one number: " 123 #"
2580 PRINT"PRINT numbers separated by ;"
2590 PRINT 1;2;3;:PRINT"#";
2600 GOSUB 9010:IF a$<>" 1  2  3 #" THEN ERROR 33
2610 PRINT"PRINT numbers separated by , (default ZONE 13)"
2620 PRINT 1,2,3;:PRINT"#";
2630 GOSUB 9010:IF a$<>" 1            2            3 #" THEN ERROR 33
2640 PRINT"PRINT numbers, computed"
2650 PRINT -1 -2 -3;"#";
2660 GOSUB 9010:IF a$<>"-6 #" THEN ERROR 33
2670 PRINT -1;-2;-3;"#";
2680 GOSUB 9010:IF a$<>"-1 -2 -3 #" THEN ERROR 33
2690 PRINT"PRINT strings separated by space"
2700 PRINT "a" "b" "c" "#";
2710 GOSUB 9010:IF a$<>"abc#" THEN ERROR 33
2720 PRINT"PRINT strings separated by ;"
2730 PRINT "a";"b";"c";"#";
2740 GOSUB 9010:IF a$<>"abc#" THEN ERROR 33
2750 PRINT"PRINT strings separated by ,"
2760 PRINT "a","b","c";"#";: '[zone 13]
2770 GOSUB 9010:IF a$<>"a            b            c#" THEN ERROR 33
2780 ZONE 5
2790 PRINT "a","b","c";"#";: '[zone 5]
2800 GOSUB 9010:IF a$<>"a    b    c#" THEN ERROR 33
2810 ZONE 13
2820 PRINT"PRINT strings separated by tab()"
2830 PRINT "a"TAB(2)"b"TAB(3)"c"TAB(4)"#";
2840 GOSUB 9010:IF a$<>"abc#" THEN ERROR 33
2850 PRINT "a"TAB(13)"b"TAB(20)"c"TAB(22)"#";
2860 GOSUB 9010:IF a$<>"a           b      c #" THEN ERROR 33
2870 PRINT "a"TAB(78)"bc#";
2880 IF POS(#0)<>1 THEN ERROR 33
2890 PRINT CHR$(8);: 'back
2900 IF POS(#0)<>80 THEN ERROR 33
2910 GOSUB 9010:IF a$<>"a                                                                            bc#" THEN ERROR 33
2920 PRINT "a"TAB(79)"bc#";
2930 GOSUB 9010:IF a$<>"bc#" THEN ERROR 33
2940 PRINT"PRINT number without separator"
2950 PRINT &X10 2;"#";
2960 GOSUB 9010:IF a$<>" 2  2 #" THEN ERROR 33
2970 GOSUB 9040
2980 '
2990 PRINT"PRINT special exponential number expressions"
3000 PRINT 1 e++4;:PRINT"#";
3010 GOSUB 9010:IF a$<>" 1  4 #" THEN ERROR 33
3020 PRINT 1 e+-4;:PRINT"#";
3030 GOSUB 9010:IF a$<>" 1 -4 #" THEN ERROR 33
3040 PRINT 50000 0.5;:PRINT"#";
3050 GOSUB 9010:IF a$<>" 50000  0.5 #" THEN ERROR 33
3060 PRINT 1 EXP(0);:PRINT"#";
3070 GOSUB 9010:IF a$<>" 1  1 #" THEN ERROR 33
3080 '
3090 PRINT"PRINT USING number format"
3100 PRINT USING "##.##";0;:PRINT"#";
3110 GOSUB 9010:IF a$<>" 0.00#" THEN ERROR 33
3120 PRINT USING "##.##";-1.2;:PRINT"#";
3130 GOSUB 9010:IF a$<>"-1.20#" THEN ERROR 33
3140 PRINT USING "##.##";1.005;:PRINT"#";
3150 GOSUB 9010:IF a$<>" 1.01#" THEN ERROR 33
3160 PRINT USING "##.##";8.575;:PRINT"#";
3170 GOSUB 9010:IF a$<>" 8.58#" THEN ERROR 33
3180 PRINT"PRINT USING number too long"
3190 PRINT USING "#.##";15.355;:PRINT"#";
3200 GOSUB 9010:IF a$<>"%15.36#" THEN ERROR 33
3210 PRINT"PRINT USING string format"
3220 PRINT USING "\   \";"n1";"n2";" xx3";:PRINT"#";
3230 GOSUB 9010:IF a$<>"n1   n2    xx3 #" THEN ERROR 33
3240 PRINT USING "!";"a1";"b2";:PRINT"#";
3250 GOSUB 9010:IF a$<>"ab#" THEN ERROR 33
3260 PRINT USING "&";"a1";"b2";:PRINT"#";
3270 GOSUB 9010:IF a$<>"a1b2#" THEN ERROR 33
3280 'gosub 9040
3290 '
3300 PRINT"ROUND"
3310 a=ROUND(PI):IF a<>3 THEN ERROR 33
3320 a=ROUND(PI,0):IF a<>3 THEN ERROR 33
3330 a=ROUND(PI,0.4):IF a<>3 THEN ERROR 33
3340 a=ROUND(PI,2):IF a<>3.14 THEN ERROR 33
3350 a=ROUND(PI,2.4):IF a<>3.14 THEN ERROR 33
3360 a=ROUND(1234.5678,-2):IF a<>1200 THEN ERROR 33
3370 a=ROUND(8.575,2):IF a<>8.58 THEN ERROR 33
3380 '
3390 GOSUB 9040
3400 PRINT"DATA and RESTORE"
3410 RESTORE 3420: READ s$,t$: IF s$+t$<>"1" THEN ERROR 33
3420 DATA 1,
3430 '
3440 PRINT"OPENIN and LINE INPUT #9"
3450 t$="testpage.dat":PRINT"OPENIN ";t$;" with characters 33..255"
3460 OPENIN "!"+t$
3470 FOR i=33 TO 255:LINE INPUT #9,t$
3480 t=ASC(t$):PRINT t$;: IF t<>i THEN PRINT"error:";i;"<>";t:ERROR 33
3490 PRINT t$;
3500 NEXT
3510 PRINT
3520 CLOSEIN
3530 PRINT
3540 '
3550 PRINT"OPENOUT, OPENIN and LINE INPUT #9"
3560 OPENOUT "!testpg2.dat"
3570 FOR i=33 TO 255:t$=CHR$(i):PRINT #9,t$
3580 NEXT
3590 CLOSEOUT
3600 OPENIN "!testpg2.dat"
3610 FOR i=33 TO 255:LINE INPUT #9,t$:PRINT t$;
3620 t=ASC(t$):IF t<>i THEN PRINT"error:";i;"<>";t:ERROR 33
3630 NEXT
3640 CLOSEIN
3650 PRINT:PRINT
3660 '
3670 PRINT"Numbers in files"
3680 OPENOUT "!testpg2.dat"
3690 FOR i=0 TO 10:PRINT#9,i:NEXT :'separate lines
3700 CLOSEOUT
3710 OPENIN "!testpg2.dat"
3720 FOR i=0 TO 10:INPUT #9,t
3730 IF i<>t THEN PRINT"error:";i;"<>";t:ERROR 33
3740 NEXT
3750 CLOSEIN
3760 OPENIN "!testpg2.dat"
3770 FOR i=0 TO 10:INPUT #9,t$
3780 a$=STR$(i)+" ":a$=RIGHT$(a$,LEN(a$)-1):IF a$<>t$ THEN PRINT"error:";a$;"<>";t$:ERROR 33
3790 NEXT
3800 CLOSEIN
3810 '
3820 OPENOUT "!testpg2.dat"
3830 FOR i=0 TO 10:PRINT#9,i;:NEXT :'one line
3840 CLOSEOUT
3850 OPENIN "!testpg2.dat"
3860 FOR i=0 TO 10:INPUT #9,t
3870 IF i<>t THEN PRINT"error:";i;"<>";t:ERROR 33
3880 NEXT
3890 CLOSEIN
3900 OPENIN "!testpg2.dat"
3910 INPUT #9,t$
3920 IF t$<>"0  1  2  3  4  5  6  7  8  9  10 " THEN PRINT"error:";:ERROR 33
3930 CLOSEIN
3940 PRINT
3950 '
3960 PRINT"Mixed style"
3970 OPENOUT "!testpg2.dat"
3980 FOR i=0 TO 10:PRINT#9,i;"&a1";&A2:NEXT
3990 CLOSEOUT
4000 OPENIN "!testpg2.dat"
4010 FOR i=0 TO 10:INPUT #9,t,t2
4020 IF i<>t THEN PRINT"error:";i;"<>";t:ERROR 33
4030 IF t2<>&A1 THEN PRINT"error:";t2;"<>";&A1:ERROR 33
4040 INPUT #9,t2
4050 IF t2<>&A2 THEN PRINT"error:";t2;"<>";&A2:ERROR 33
4060 NEXT
4070 CLOSEIN
4080 '
4090 PRINT"Separated with comma"
4100 OPENOUT "!testpg2.dat"
4110 FOR i=0 TO 25:PRINT#9,i+65,CHR$(i+65):NEXT
4120 CLOSEOUT
4130 OPENIN "!testpg2.dat"
4140 FOR i=0 TO 25:INPUT #9,t,t$
4150 IF i+65<>t THEN PRINT"error:";i+65;"<>";t:ERROR 33
4160 IF t$<>CHR$(i+65) THEN PRINT"error:";t$;"<>";CHR$(i+65):ERROR 33
4170 NEXT
4180 CLOSEIN
4190 '
4200 PRINT"WRITE and INPUT"
4210 OPENOUT "!testpg2.dat"
4220 FOR i=0 TO 25:WRITE #9,i+65,CHR$(i+65):NEXT
4230 CLOSEOUT
4240 OPENIN "!testpg2.dat"
4250 FOR i=0 TO 25:INPUT #9,t,t$
4260 IF i+65<>t THEN PRINT"error:";i+65;"<>";t:ERROR 33
4270 IF t$<>CHR$(i+65) THEN PRINT"error:";t$;"<>";CHR$(i+65):ERROR 33
4280 NEXT
4290 CLOSEIN
4300 '
4310 '
4320 PRINT"|ERA to delete testpg2.dat (CPC: DISC only)"
4330 |ERA,"testpg2.dat"
4340 '
4350 GOSUB 9040
4360 '
4370 PRINT"SYMBOL AFTER"
4380 a=240:h=HIMEM+(256-a)*8
4390 a=256:SYMBOL AFTER a:IF HIMEM<>h-(256-a)*8 THEN PRINT"error:";HIMEM;"<>";h:ERROR 33
4400 a=0:SYMBOL AFTER a:IF HIMEM<>h-(256-a)*8 THEN PRINT"error:";HIMEM;"<>";h:ERROR 33
4410 a=240:SYMBOL AFTER a:IF HIMEM<>h-(256-a)*8 THEN PRINT"error:";HIMEM;"<>";h:ERROR 33
4420 MEMORY HIMEM-1
4430 ON ERROR GOTO 4440:SYMBOL AFTER 241:PRINT"Error expected!":ERROR 33: 'expect error 5
4440 IF ERR<>5 THEN PRINT"err=";ERR;"erl=";ERL:ERROR 33 ELSE RESUME 4450
4450 ON ERROR GOTO 0
4460 MEMORY HIMEM+1
4470 PRINT"UNT"
4480 a=UNT(32767):IF a<>32767 THEN ERROR 33
4490 a=UNT(32768):IF a<>-32768 THEN ERROR 33
4500 a=UNT(65535):IF a<>-1 THEN ERROR 33
4510 PRINT"VAL"
4520 a=VAL(""):IF a<>0 THEN ERROR 33
4530 a=VAL("4r"):IF a<>4 THEN ERROR 33
4540 a=VAL("&ff"):IF a<>&FF THEN ERROR 33
4550 '
4560 GOSUB 9040
4570 '
4580 PRINT "stairs"
4590 FOR i=1 TO 24:PRINT STRING$(i*2, "O"):NEXT
4600 MOVE 0,350
4610 FOR n=1 TO 8
4620 DRAWR 50,0
4630 DRAWR 0,-50
4640 NEXT
4650 MOVE 348,0
4660 FILL 1
4670 '
4680 PRINT "test finished: ok"
4690 END
4700 '
9000 'get characters from screen; print crlf
9010 a$="":i=1:WHILE i<=80 AND RIGHT$(a$,1)<>"#":LOCATE i,VPOS(#0):a$=a$+COPYCHR$(#0):i=i+1:WEND:PRINT:RETURN
9020 '
9030 'wait some time or for key press
9040 t!=TIME+6*50:a$="":WHILE TIME<t! AND a$="":a$=INKEY$:WEND
9050 PRINT:PRINT STRING$(10, "-"):PRINT
9060 RETURN
9070 '
*/ });
